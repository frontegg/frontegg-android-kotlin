plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}


group 'com.frontegg.android'
version '1.2.38'


android {
    namespace 'com.frontegg.android'
    compileSdk 34
    compileSdkVersion 34

    defaultConfig {
        minSdk 26
        targetSdk 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        versionName "$version"
    }

    sourceSets {
        androidTest.manifest.srcFile "src/androidTest/AndroidManifest.xml"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildToolsVersion '34.0.0'

    publishing {
        singleVariant("release") {
            // if you don't want sources/javadoc, remove these lines
            withSourcesJar()
            withJavadocJar()
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
}


ext {
    coroutinesVersion = '1.7.3'
    androidCredentialsVersion = "1.3.0"
}


dependencies {

    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'io.reactivex.rxjava3:rxkotlin:3.0.1'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    implementation 'androidx.webkit:webkit:1.12.1'
    implementation "androidx.browser:browser:1.8.0"
    implementation 'com.google.androidbrowserhelper:androidbrowserhelper:2.5.0'

    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation 'androidx.lifecycle:lifecycle-process:2.6.2'

    // needed for encrypted shared preferences
    implementation('androidx.security:security-crypto:1.1.0-alpha06') {
        exclude group: 'com.google.crypto.tink', module: 'tink-android'
    }
    implementation 'com.google.crypto.tink:tink-android:1.9.0'

    // optional - needed for credentials support from play services, for devices running Android 13 and below.
    implementation "androidx.credentials:credentials-play-services-auth:$androidCredentialsVersion"
    implementation "androidx.credentials:credentials:$androidCredentialsVersion"




    // Testing
    testImplementation 'androidx.test:core-ktx:1.6.1'
    testImplementation 'androidx.test.ext:junit-ktx:1.2.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.14.1'
    testImplementation "io.mockk:mockk:1.13.13"
    testImplementation "org.junit.jupiter:junit-jupiter:5.11.3"
    testImplementation 'org.json:json:20180813'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    testImplementation "androidx.arch.core:core-testing:2.2.0"
}

afterEvaluate {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId 'com.frontegg.sdk'
                artifactId 'android'
                version = android.defaultConfig.versionName
                from components.release

                pom {
                    // Signing configuration
                    signing {
                        def gpgPrivateKey = System.getenv('GPG_PRIVATE_KEY')
                        def gpgPassphrase = System.getenv("GPG_PASSPHRASE")

                        useInMemoryPgpKeys("0D80D868", gpgPrivateKey, gpgPassphrase)
                        sign publishing.publications.mavenJava
                    }

                    name = 'android'
                    description = 'Frontegg Android Kotlin'
                    url = 'https://github.com/frontegg/frontegg-android-kotlin'

                    organization {
                        name = 'Frontegg LTD'
                        url = 'https://frontegg.com'
                    }
                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/frontegg/frontegg-android-kotlin/issues'
                    }
                    licenses {
                        license {
                            name = 'The MIT License (MIT)'
                            url = 'https://raw.githubusercontent.com/frontegg/frontegg-android-kotlin/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'frontegg'
                            name = 'Frontegg Support'
                            email = 'support@frontegg.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/frontegg/frontegg-android-kotlin.git'
                        developerConnection = 'scm:git:ssh://github.com/frontegg/frontegg-android-kotlin.git'
                        url = 'https://github.com/frontegg/frontegg-android-kotlin'
                    }
                }


            }
        }
        repositories {
            maven {
                name = "OSSRH"
                url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                credentials {
                    username = System.getenv('NEXUS_USERNAME')
                    password = System.getenv('NEXUS_PASSWORD')
                }
            }
        }
    }
}